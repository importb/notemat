plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}


group = 'com.notemat'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

def osName = System.getProperty('os.name').toLowerCase()
def javafxPlatform = 'linux'
if (osName.contains('win')) {
    javafxPlatform = 'win'
} else if (osName.contains('mac')) {
    javafxPlatform = 'mac'
}

dependencies {
    // JavaFX dependencies
    implementation "org.openjfx:javafx-base:20.0.2:${javafxPlatform}"
    implementation "org.openjfx:javafx-graphics:20.0.2:${javafxPlatform}"
    implementation "org.openjfx:javafx-controls:20.0.2:${javafxPlatform}"
    implementation "org.openjfx:javafx-fxml:20.0.2:${javafxPlatform}"
    implementation "org.openjfx:javafx-swing:20.0.2:${javafxPlatform}"

    // Other UI and utility dependencies
    implementation 'org.fxmisc.richtext:richtextfx:0.11.0'
    implementation 'org.json:json:20231013'

    // Google Generative AI SDK
    implementation 'com.google.genai:google-genai:0.3.0'
}

shadowJar {
    archiveBaseName.set('notemat')
    archiveClassifier.set('')
    archiveVersion.set('')
    mergeServiceFiles()
    manifest {
        attributes 'Main-Class': 'com.notemat.Launcher'
    }

    exclude 'META-INF/*.SF'
    exclude 'META-INF/*.DSA'
    exclude 'META-INF/*.RSA'
}

tasks.build {
    dependsOn shadowJar
}

tasks.jar {
    enabled = false
}
